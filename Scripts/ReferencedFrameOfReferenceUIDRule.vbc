Class ReferencedFrameOfReferenceUIDRule
    Inherits MessageCollectionRule

    Public Sub New(ByVal iodType As IODType, ByVal severity As ErrorSeverity, ByVal ctSet As CTSet)

        MyBase.New("ReferencedFrameOfReferenceUIDRule", iodType, severity)
        m_ctSet = ctSet

    End Sub

    Private m_ctSet As CTSet
    Private m_messageCollectionHelper As messageCollectionHelper


    Public Overrides Sub ApplyRule(ByRef dataSetCollection As DvtkHighLevelInterface.Dicom.Messages.DicomMessageCollection)
        m_messageCollectionHelper = New messageCollectionHelper(dataSetCollection)

        Dim structureSetDataSet As DvtkHighLevelInterface.Dicom.Other.DataSet = Nothing
        Dim valid As Boolean = False
        Dim message As String

        If m_messageCollectionHelper.GetStructureSet() Is Nothing Then
            valid = False
            message = "No RT Structure Set received"
        Else
            structureSetDataSet = m_messageCollectionHelper.GetStructureSet()
            Dim frameOfReferenceAttribute As DvtkHighLevelInterface.Dicom.Other.Attribute = Nothing
            Dim referencedFrameOfReferenceUIDAttribute As DvtkHighLevelInterface.Dicom.Other.Attribute = Nothing
            Dim structureSetROITagSequenceAttribute As DvtkHighLevelInterface.Dicom.Other.Attribute = Nothing

            Dim seqItem As DvtkHighLevelInterface.Dicom.Other.SequenceItem = Nothing
            Dim ctImageDataSet As DvtkHighLevelInterface.Dicom.Other.DataSet = Nothing

            'Dim structureSetROITagSequence As String = "0x30060020"
            'Dim referencedFrameOfReferenceUIDTagSequence As String = "0x30060024"
            'Dim frameOfReferenceTagSequence As String = "0x00200052"

            Dim referencedFrameOfReferenceUIDValue As String
            Dim frameOfReferenceUIDValue As String

            Dim done As Boolean = False
            Dim ctImageReceveid As Boolean = False


            If m_ctSet = CTSet.SingleSerieCTSet Then
                'Get first image of single ct series from the ReferenceDataSet, so the send ct images
                ctImageDataSet = ReferenceDataSet.GetInstance().GetCTImage(1, 1).DataSet
            Else
                'Get first image of multi ct series from the DataSetCollection, so from the received ct images
                ctImageDataSet = m_messageCollectionHelper.GetSliceImage(IODType.CTImage, 1)
            End If

            If ctImageDataSet Is Nothing Then
                ctImageReceveid = False
            Else
                ctImageReceveid = True
            End If

            If ctImageReceveid = True Then

                'Get frameOfReference from the ct image
                frameOfReferenceAttribute = ctImageDataSet(Tags.FrameofReferenceUID)
                frameOfReferenceUIDValue = frameOfReferenceAttribute.Values(0)

                'Check if Structure Set ROI Sequence is present
                If structureSetDataSet.Exists(Tags.StructureSetROITagSequence) = False Then
                    valid = False
                    message = "The attribute: '" + Tags.GetTagName(Tags.StructureSetROITagSequence) + "' (" + Tags.StructureSetROITagSequence + ") is not present."
                Else
                    'Get Structure Set ROI Sequence from dataset
                    structureSetROITagSequenceAttribute = structureSetDataSet(Tags.StructureSetROITagSequence)
                    Dim i As Integer
                    'Itterate through the items

                    If structureSetROITagSequenceAttribute.ItemCount() = 0 Then
                        valid = False
                        message = "'" + Tags.GetTagName(Tags.StructureSetROITagSequence) + "' (" + Tags.StructureSetROITagSequence + ") does not contain items"
                    Else
                        For i = 1 To structureSetROITagSequenceAttribute.ItemCount()
                            seqItem = structureSetROITagSequenceAttribute.GetItem(i)
                            'Get referencedFrameOfReferenceUID Value
                            referencedFrameOfReferenceUIDValue = seqItem.GetValues(Tags.ReferencedFrameOfReferenceUIDTagSequence)(0)

                            'If one referencedFrameOfReferenceUIDValue is not equal to frameOfReferenceUIDValue
                            'Then stop searching
                            If done = False Then
                                If Not referencedFrameOfReferenceUIDValue = frameOfReferenceUIDValue Then
                                    done = True
                                    valid = False
                                    message = "'" + Tags.GetTagName(Tags.ReferencedFrameOfReferenceUIDTagSequence) + "' (" + Tags.ReferencedFrameOfReferenceUIDTagSequence + ") not equal to '" + Tags.GetTagName(Tags.FrameofReferenceUID) + "' (" + Tags.FrameofReferenceUID + ")"
                                Else
                                    valid = True
                                End If
                            End If
                        Next
                    End If
                End If
            Else
                valid = False
                message = "No CT-Images received"
            End If
        End If

        If valid = True Then
            message = "'" + Tags.GetTagName(Tags.ReferencedFrameOfReferenceUIDTagSequence) + "' (" + Tags.ReferencedFrameOfReferenceUIDTagSequence + ") equal to '" + Tags.GetTagName(Tags.FrameofReferenceUID) + "' (" + Tags.FrameofReferenceUID + ")"
            Me.SetValid(message)
        Else
            Me.SetError(message)
        End If

        'Reporter, write to results, if there is anything to report
        Me.Report()
    End Sub
End Class