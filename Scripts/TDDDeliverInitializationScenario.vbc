Class TDDDeliveryInitializationScenario
    Inherits Scenario

    'Constructor
    Public Sub New()
        Dim tmsActor As TMSActor = New TMSActor
        Dim tddActor As TDDActor = New TDDActor()

        Dim treatmentForm As ChooseKindOfTreatmentDialog = New ChooseKindOfTreatmentDialog()
        Dim treatmentType As String = String.Empty
        Dim errorDialog As ErrorDialog = New ErrorDialog()
        Dim isResumeTreatmentDialog As ResumeTreatmentDialog = New ResumeTreatmentDialog()
        Dim queryForm As EditQueryForm = New EditQueryForm()

        Try
            ' Check if dataset is present
            DataSetHandler.CheckUpsDataSets()

            Try
                DataSetHandler.CheckUPSMessageExists(DataSetHandler.UPSCacheXMLFileName, "C-FIND-RSP")
            Catch ex As Exception
                Dim errorMessage As String = "First run the 'TDD Retrieval of Worklist testscenario' folowed by the 'TDD Retrieve Static Treatment Delivery Input Instances testscenario', at least once."
                errorDialog.SetMessage(errorMessage)
                errorDialog.ShowDialog()
                Throw ex
            End Try

            'remove cached datasets (if any)
            Try
                DataSetHandler.DeleteDatasetFile(DataSetHandler.UPSCacheXMLFileName, "RtBeamDeliv")
            Catch ex As Exception
                'do nothing - if not present everything is fine
            End Try

            'Detect treatment type
            treatmentForm.ShowDialog()

            If treatmentForm.DialogResult = System.Windows.Forms.DialogResult.Yes Then
                Dim datasetPath As String = DataSetHandler.GetDataPath(DataSetHandler.UPSDataXMLFileName)

                'load RT Plan dataset
                Dim dataset As DvtkHighLevelInterface.Dicom.Other.DataSet = DataSetHandler.LoadDatasetFromFolderByClassUid(datasetPath, SOPclass.RTPlanDosimetricSOPClassUID)
                'extract number of beams from RT Plan
                isResumeTreatmentDialog.addMessage(dataset)
                isResumeTreatmentDialog.CheckRTPlan()

                'load RT beam delivery dataset
                dataset = DataSetHandler.LoadDatasetFromFolderByClassUid(datasetPath, SOPclass.RTBeamsDeliveryInstructionSOPClassUID)
                'incorporate number of beams and other information in RT Beam Delivery
                isResumeTreatmentDialog.addMessage(dataset)
                isResumeTreatmentDialog.ShowDialog()
            End If

            'RO-60: Treatment Delivery in Progress            
            Dim ro60transaction As RO60Transaction = New RO60Transaction()
            'RO-61: Retrieve Dynamic Treatment Delivery Input Instances from TMS            
            Dim ro61transaction As RO61Transaction = New RO61Transaction(tmsActor, tddActor)

            'Configure the receive step: set the cstore thread as part of the C-Move command
            'Configure the UPS In Progress response
            'Add transaction steps
            Dim receive As ReceiveScenarioStep = New ReceiveScenarioStep(New TMSActor(), New TDDActor(), True)
            receive.AddTransaction(ro60transaction)
            receive.AddTransaction(ro61transaction)

            'Configure the validatRO60 request step
            Dim validateRO60 As ValidateScenarioStep = New ValidateScenarioStep(receive, 0)
            validateRO60.AddRules(ro60transaction.GetCommonTransactionRulesForRequest())

            'Configure the validateRO61 step
            Dim validateRO61 As ValidateScenarioStep = New ValidateScenarioStep(receive, 1)
            validateRO61.AddRules(ro61transaction.GetCommonTransactionRulesForRequest())
            validateRO61.AddRules(ro61transaction.GetTransactionRulesForRequest())

            'Add the steps to the list
            m_scenarioSteps.Add(receive)
            m_scenarioSteps.Add(validateRO60)
            m_scenarioSteps.Add(validateRO61)

        Catch ex As Exception
            errorDialog.SetMessage(ex.Message)
            errorDialog.ShowDialog()
            Throw ex
        End Try

    End Sub
End Class
